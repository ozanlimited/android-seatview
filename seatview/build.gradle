buildscript {
    repositories {
        mavenCentral()
    }
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    mGroupId = "com.ozanlimited"
    mArtifactId = "seatview"
    mVersionCode = 1
    mVersionName = "1.0.0"

    mLibraryName = "Seat-View"
    mLibraryDescription = ""
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId mGroupId
                artifactId mArtifactId
                version mVersionName

                from components.release

                artifact androidSourcesJar

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }

        repositories {
            maven {
                url System.getenv("GITLAB_REPO_URL")
                credentials(HttpHeaderCredentials) {
                    name = System.getenv("GITLAB_DEPLOY_KEY")
                    value = System.getenv("GITLAB_DEPLOY_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }

    publish.dependsOn assemble

    dependencies {
        implementation fileTree(include: ['*.jar'], dir: 'libs')
        implementation 'androidx.annotation:annotation:1.4.0'
        def core_version = "1.8.0"

        // Java language implementation
        implementation "androidx.core:core:$core_version"
    }
}
